#!/usr/bin/perl

use strict;
use warnings;

use File::Basename;
use File::Path 'make_path';
use Getopt::Std;
use IPC::Open2;
use POSIX qw(setsid);
use Text::ParseWords;

use Digest::SHA 'sha256';

our ($opt_g, $opt_d, $opt_r);
my $gpg;

# usage: print usage information to stderr and exit with error.
sub usage {
	die
"usage: gpm [-g command] [-d dir] add [-Nnm] name\n" .
"       gpm [-g command] [-d dir] copy [-Nnm] [-d delcmd] [-i incmd]\n" .
"                                      [-o outcmd] [-s time] name\n" .
"       gpm [-d dir] rm name ...\n" .
"       gpm [-g command] [-d dir] show [-Nn] name\n" .
"       gpm [-d dir] mv from to\n" .
"       gpm [-d dir] ls\n";
}

# getflags: put environment flags at the start of @ARGV
sub getflags {
	my ($var) = @_;
	unshift(@ARGV, shellwords $ENV{$var} // '') if defined $var && $var ne '';
}

# shellquote: return string suitable for using as an argument for sh
sub shellquote {
	my ($s) = @_;
	$s =~ s/'/'\\''/g;
	return "'" . $s . "'";
}

# getrecipient: return string to be used with gpg's -r option.
#
# Past versions of gpm (written in shell) required explicit recipient, set by
# option -r, or the GPM_RECIPIENT environment variable. This is completely
# unnecessary now, due to GPM_GPG and -g, but legacy syntax is still maintained.
sub getrecipient {
	return shellquote($opt_r // $ENV{GPM_RECIPIENT});
}

# ckpath $path
#
# Return 1 if $path doesn't start with /, or contain .. files, and 0 otherwise.
sub ckpath {
	my ($p) = @_;
	return $p !~ m,^/, && $p ne ".." && $p !~ m,^\.\./, && $p !~ m,/\.\./,
	    && $p !~ m,/\.\.$,;
}

# cklegacy $f
#
# Returns the correct path for $f (with or without the .gpg suffix), or dies
# if no such file exists.
sub cklegacy {
	my ($f) = @_;
	if (!-f $f) {
		my $f2 = $f . ".gpg";
		-f $f2 or die "neither $f, nor $f2 exist as regular files\n";
		return $f2;
	}
	return $f;
}

# prunetree $d: remove empty directories, starting from $d, and going up.
sub prunetree {
	for (my ($d) = @_; $d ne "."; $d = dirname $d) {
		rmdir $d or last;
	}
}

# add
#
# Encrypt the secret from stdin, and store the ciphertext in file specified
# on the command line.
sub add {
	our ($opt_N, $opt_n, $opt_m);
	my ($r, $sec, $cmd) = (getrecipient(), '', "$gpg -e");

	$cmd .= " -r $r" if defined $r;

	getopts('Nnm') or usage();
	$#ARGV >= 0 or usage();

	my $outfile = $ARGV[0];
	ckpath $outfile or die "bad path: $outfile\n";

	-e $outfile and die "$outfile already exists\n";

	if (-t STDIN && !$opt_m) {
		system "stty -echo";

		print "Secret:";
		$sec = <STDIN>;
		print "\n";
		print "Repeat:";
		my $sec2 = <STDIN>;
		print "\n";
		if ($opt_n && !$opt_N) {
			chomp $sec;
			chomp $sec2;
		}

		system "stty echo";
		die "Sorry\n" if $sec ne $sec2;
		undef $sec2;
	} else {
		while (<STDIN>) { $sec .= $_; }
	}

	my $pid = open2(my $reader, my $writer, $cmd);
	print $writer $sec;
	undef $sec;
	close $writer;
	my $out = '';
	while (<$reader>) { $out .= $_; }
	waitpid $pid, 0;
	$? == 0 or exit 1;

	my $d = dirname $outfile;
	make_path $d, {mode => 0700};
	umask 0377;
	unless (open FH, ">$outfile") {
		prunetree $d;
		die "couldn't open $outfile for writing: $!\n";
	}
	unless (print FH $out) {
		prunetree $d;
		die "couldn't write to $outfile: $!\n";
	}
}

# ls
#
# Produce a listing of files in the gpm directory, using the GPM_LSCMD
# environment variable if defined.
sub ls {
	my $cmd = $ENV{GPM_LSCMD};

	unless (defined $cmd) {
		opendir my $dh, "." or die "couldn't open directory .: $!\n";
		while (readdir $dh) {
			if ($_ ne "." && $_ ne ".." && -d $_) {
				$cmd = "find . -type f | sed 's,^\./,,'";
				last;
			}
		}
		$cmd = "ls" unless defined $cmd;
	}
	system $cmd;
	$? == 0 or exit 1;
}

# mv: safely rename $ARGV[0] to $ARGV[1].
sub mv {
	$#ARGV >= 1 || usage();
	my ($from, $to) = @ARGV;

	ckpath $from or die "bad path $from\n";
	ckpath $to or die "bad path $to\n";
	-e $to and die "$to already existst\n";
	$from = cklegacy $from;

	make_path(dirname $to, {mode => 0700});
	rename $from, $to;
	prunetree(dirname $from);
}

# rm: unlink arguments, asking each time.
sub rm {
	$#ARGV >= 0 || usage();
	for (@ARGV) {
		my $f = $_;
		unless (ckpath $f) {
			print STDERR "bad path $f\n";
			next;
		}
		$f = cklegacy $f;
		print "Really remove $f? ";
		<STDIN> =~ m/^[Yy]/ and unlink $f;
		prunetree(dirname $f);
	}
}

# get: decrypt file, and return plaintext.
sub get {
	my ($Nflag, $nflag) = @_;
	$#ARGV >= 0 or usage();
	my $file = $ARGV[0];

	ckpath $file or die "bad path $file\n";
	$file = shellquote(cklegacy $file);
	my $out = `$gpg -d $file`;
	$? == 0 or exit 1;
	chomp($out) if ($nflag && !$Nflag);

	return $out;
}

# show: decrypt file, and print plaintext to stdout.
sub show {
	our ($opt_N, $opt_n);
	getopts('Nn') or usage();
	print(get $opt_N, $opt_n);
}

# copy: decrypt file, and copy to GPM_COPY_INCMD, delete with GPM_COPY_DELCMD
# after GPM_COPY_SLEEP seconds if necessary.
sub copy {
	our ($opt_N, $opt_n, $opt_d, $opt_i, $opt_o, $opt_s);

	getopts('Nnd:i:o:s:') or usage();
	$#ARGV >= 0 or usage();

	my $delcmd =	$opt_d // $ENV{GPM_COPY_DELCMD} // "xclip </dev/null";
	my $incmd =	$opt_i // $ENV{GPM_COPY_INCMD} // "xclip";
	my $outcmd =	$opt_o // $ENV{GPM_COPY_OUTCMD} // "xclip -o";
	my $sleep =	$opt_s // $ENV{GPM_COPY_SLEEP} // 60;

	my $pw = get $opt_N, $opt_n;

	# This is a huge cludge. The reason we have to do copying inside a detached
	# process is because otherwise the following doesn't work (assuming xclip):
	#
	# $ tmux popup -E 'GPG_TTY=`tty` pmc' && sleep 1 && xclip -o
	my $pid = fork();
	if (not defined $pid) {
		die "Fork failed: $!\n";
	} elsif ($pid == 0) {
		POSIX::setsid();

		open(FH, "|-", $incmd) or
		    die "Could not open command '$incmd': $!\n";
		print FH $pw;
		close FH;
		if ($? != 0) {
			system $delcmd;
			exit 1;
		}

		if ($sleep < 1) {
			exit 0;
		}

		$pw = sha256 $pw;
		$pid = fork();
		if (not defined $pid) {
			system $delcmd;
			die "Fork failed: $!\n";
		} elsif ($pid == 0) {
			open(STDOUT, '>/dev/null');
			open(STDERR, '>/dev/null');
			POSIX::setsid();
			sleep $sleep;
			system($delcmd) if
			    (sha256(`$outcmd`) eq $pw);
		}
	} else {
		waitpid $pid, 0;
		exit $?;
	}
}

getopts('g:d:r') or usage();

$#ARGV >= 0 or usage();
my $cmd = $ARGV[0];
shift @ARGV;

$gpg = $opt_g // $ENV{GPM_GPG} // "gpg";

my $gpmd = $opt_d // $ENV{GPM_DIR};
unless (defined $gpmd) {
	if (defined $ENV{XDG_DATA_HOME}) {
		$gpmd = $ENV{XDG_DATA_HOME} . "/gpm";
	} elsif (defined $ENV{HOME}) {
		$gpmd = $ENV{HOME} . "/.gpm";
	} else {
		die "couldn't determine the gpm directory\n";
	}
}

make_path $gpmd, {mode => 0700};
chdir $gpmd or die "couldn't change directory to $gpmd: $!\n";

for ($cmd) {
if (/^a/) { add(getflags "GPM_ADD"); last; }
if (/^c/) { copy(getflags "GPM_COPY"); last; }
if (/^l/) { ls(getflags "GPM_LS"); last; }
if (/^m/) { mv(getflags "GPM_MV"); last; }
if (/^r/) { rm(getflags "GPM_RM"); last; }
if (/^s/) { show(getflags "GPM_SHOW"); last; }
	usage();
}
